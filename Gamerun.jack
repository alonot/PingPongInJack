class Gamerun {
    field Slider slider1,slider2,surpriseSlider;
    field Ball ball;
    field int direction,scr_ai,scr_py,no_ball,directionAI;
    field int surpriseCounter;
    
    /** initializer */
    constructor Gamerun new(){
        do Gui();
        let slider1 = Slider.new(17,95,55,8,2);
        let slider2 = Slider.new(486,95,45,8,2);
        let ball= Ball.new(255,127,4);
        let direction = 0;
        let directionAI=0;
        let surpriseCounter=0;
        let scr_ai =0;
        let scr_py =0;
        let no_ball=17;
        
        return this;
    }

    /** Draws the gui to the screen */
    method void Gui() {
        do Screen.setColor(true);
        do Screen.drawLine(15,15,496,15);
        do Screen.drawLine(496,15,496,240);
        do Screen.drawLine(15,240,496,240);
        do Screen.drawLine(15,15,15,240);
        do Output.printString("No.ball . . . . .");
        do Output.moveCursor(22,0);
        do Output.printString("Score of AI: 0   ");
        do Output.printString("Score of Player: 0");
        return;
        // 22,14 AI
        // 22, 35 Player
    }

    /** changes the score and  shows it to the screen */
    method void score(){
        do Output.moveCursor(22,14);
        do Output.backSpace();
        do Output.printInt(scr_ai);
        do Output.moveCursor(22,35);
        do Output.backSpace();
        do Output.printInt(scr_py);
        do Output.moveCursor(0,no_ball);
        do Output.backSpace();
        do Output.backSpace();
        let no_ball = no_ball-2;
        return;
        
    }

    /** moves the ball and return result : whether ball is lost or collided */
    method int moveBall(){
        var Array a,b;
        var int result,randomy,temp;
        let a = slider1.ycordinate();
        let b = slider2.ycordinate();
        let result = ball.move(23,487,directionAI,direction,a,b);
        
        if (surpriseCounter > 500){
            if(surpriseCounter =501){
                let temp=((ball.gety()-((ball.gety()/10)*10)*12));
                let randomy=127+(directionAI*temp);
                if (randomy > 184){
                    let randomy=184;
                }
                if(randomy < 71){
                    let randomy=71;
                }
                let surpriseSlider=Slider.new(255,randomy,55,1,0);
            }
            if(surpriseCounter=1000){
                let surpriseCounter=0;
                do surpriseSlider.dispose();
            }else{
                let a= surpriseSlider.ycordinate();
                do ball.handleBarrier(255,256,a);
                do surpriseSlider.draw();
            }
        }
        let surpriseCounter=surpriseCounter+1;
        do a.dispose();
        do b.dispose();
        return result;
    }

    /** moves ai */
    method void Moveai(){
        var int dir;
        let dir = ball.getDirection();
        if(dir < 0){
            let directionAI=-1;
            do slider1.moveup();
        }
        if(dir >0){
            let directionAI=1;
            do slider1.movedown();
        }
        return;
    }

    /** deAlloc the memory */
    method void dispose(){
        do slider1.dispose();
        do slider2.dispose();
        return;
    }

    /** moves the slider of player */
    method void moveslider2(){
        if (direction = -1){do slider2.moveup();}
        if (direction = 1){do slider2.movedown();}
        do Sys.wait(13);
        return ;
    }

    /** Main function which starts and ends the game */
    method void run(){
        var char key;
        var int result;
        var boolean exit;
        let exit = false;

        while(~exit){
            while(key = 0){
                let key = Keyboard.keyPressed();
                do moveslider2();
                do Moveai();
                let result= moveBall();
                do slider1.draw();
                do slider2.draw();
                if(~(result=0)){
                    if(result=1){
                        let scr_ai=scr_ai+1;
                    }else{
                        let scr_py=scr_py+1;
                    }
                    do score();
                    let direction=0;
                    while(~(key=32)){
                        let key = Keyboard.keyPressed();
                        if(no_ball<8){
                            let key=32;
                        }
                    }
                    do ball.reset();
                }
                if(no_ball<8){
                    let exit=true;
                    let key=100;
                }
            }
            if (key = 81)  { let exit = true; } 
            if (key = 131) { let direction = -1; }  
            if (key = 133) { let direction = 1; }  
            let key=0;
            if(no_ball<8){
                let exit=true;
                let key=100;
            }
        
         }
         return;
    }
    
    
}